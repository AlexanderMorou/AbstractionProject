#include "root.oilexer";
/* *
 * From the C# LANGUAGE SPECIFICATION - 7.15 Query expressions
 * *
 * Simplified in areas for CST generation.
 * */
LINQExpression ::= 
    LINQFromClause:From; LINQBody:Body;;

LINQFromClause ::=
    "from" TypeReference:Type;? Identifier:Name; "in" Expression:Source;;

LINQBody ::=
    LINQBodyClause:Clauses;* 
    (
        "select" Expression:Selection; |
        "group"  Expression:Selection; "by" Expression:Key;
    ) 
    ("into" Identifier:Target; LINQBody:Next;)?;

LINQBodyClause ::=>
    LINQFromClause |
    LINQLetClause |
    LINQWhereClause |
    LINQJoinClause |
    LINQOrderByClause;

LINQLetClause ::=
    "let" Identifier:Name; '=' Expression:Expression;;

LINQWhereClause ::= 
    "where" Expression:BoolExpression;;

LINQJoinClause ::=
    "join" TypeReference:Type;? Identifier:Name; "in" Expression:TargetIn; "on" Expression:ConditionLeft; "equals" Expression:ConditionRight; ("into" Identifier:TargetOut;)?;

LINQOrderByClause ::=
    "orderby" DelimitedList<LINQOrderingClause:Orderings;, ','>;

LINQOrderingClause ::=
    Expression:OrderKey; ("ascending" | "descending"):Direction;?;
